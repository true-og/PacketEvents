import xyz.jpenilla.runpaper.RunPaperExtension
import xyz.jpenilla.runpaper.task.RunServer

plugins {
    packetevents.`shadow-conventions`
    packetevents.`library-conventions`
    packetevents.`publish-conventions`
    xyz.jpenilla.`run-paper`
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://repo.viaversion.com/everything/")
}

dependencies {
    compileOnly(libs.netty)
    shadow(libs.bundles.adventure)
    compileShadowOnly(libs.bstats.bukkit)
    shadow(project(":api", "shadow"))
    shadow(project(":netty-common"))

    compileOnly("io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT")
    compileOnly(libs.via.version)
}

tasks {
    shadowJar {
        archiveFileName = "PacketEvents-${rootProject.ext["versionNoHash"]}.jar"

        // Paper doesn't need to map spigot -> mojang since we support both
        manifest {
            attributes["paperweight-mappings-namespace"] = "mojang"
        }
    }

    // 1.8.8 - 1.16.5 = Java 8
    // 1.17           = Java 16
    // 1.18 - 1.20.4  = Java 17
    // 1.20.5+        = Java 21
    val version = "1.21.11"
    val javaVersion = JavaLanguageVersion.of(21)

    val jvmArgsExternal = listOf(
        "-Dcom.mojang.eula.agree=true"
    )

    withType<Javadoc> {
        // javadoc generation tries to load some random nms classes and fails
        exclude("io/github/retrooper/packetevents/util/protocolsupport/**")
    }

    named<RunServer>("runServer") {
        minecraftVersion(version)
        runDirectory = rootDir.resolve("run/paper/$version")

        javaLauncher = project.javaToolchains.launcherFor {
            languageVersion = javaVersion
        }

        jvmArgs = jvmArgsExternal
    }

    configure<RunPaperExtension> {
        folia.registerTask {
            minecraftVersion(version)
            runDirectory = rootDir.resolve("run/folia/$version")

            javaLauncher = project.javaToolchains.launcherFor {
                languageVersion = javaVersion
            }

            jvmArgs = jvmArgsExternal
        }
    }
}
